import org.jlab.CombineFileTask

plugins {
    id 'war'
    id ("org.gradlewebtools.minify") version "1.3.0"
}

apply plugin: org.jlab.CombinePlugin

description = "Web Extensible Display Manager"
group 'org.jlab'
version = '1.8.0'

ext {
    releaseDate = 'Aug 25 2021'
    productionRelease = 'true'
}

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile) {
    options.release = 8
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

dependencies {
    implementation 'javax.servlet:jstl:1.2'
    implementation 'org.glassfish:javax.json:1.1.4'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.1'
    providedCompile 'javax:javaee-api:7.0'
}

task combineCss(type: CombineFileTask) {
    source.from("src/main/webapp/resources/widgets").include('**/*.css')
    dest = file("${buildDir}/combined-css/combined.css")
}

task combineJs(type: CombineFileTask) {
    source.from("src/main/webapp/resources/widgets").include('**/*.js')
    dest = file("${buildDir}/combined-js/combined.js")
}

task cssMinifyIt(type: org.gradlewebtools.minify.CssMinifyTask) {
    dependsOn(combineCss)
    srcDir = project.file("build/combined-css")
    dstDir = project.file("build/minified")
}

task jsMinifyIt(type: org.gradlewebtools.minify.JsMinifyTask) {
    dependsOn(combineJs)
    srcDir = project.file("build/combined-js")
    dstDir = project.file("build/minified")
}

war {
    dependsOn(cssMinifyIt)
    dependsOn(jsMinifyIt)
    archiveFileName = 'wedm.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", project.version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
        filter {
            String line -> line.replaceAll("@PRODUCTION_RELEASE@", productionRelease)
        }
    }
    from('build/minified') {
        include '*.min.css'
        into 'resources/css/'
    }
    from('build/minified') {
        include '*.min.js'
        into 'resources/js/'
    }
}