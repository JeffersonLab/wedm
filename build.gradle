plugins {
    id 'war'
    id 'org.gradlewebtools.minify' version '2.1.1'
    id 'org.jlab.cat' version '1.1.0'
    id 'com.diffplug.spotless' version '7.2.1'
}

description = "Web Extensible Display Manager"
group = 'org.jlab'
version = new File("${projectDir}/VERSION").text.trim()
ext.version = project.version
ext.releaseDate = new Date().format('MMM dd yyyy')

ext.productionRelease = 'true'

repositories {
    mavenCentral()
}

tasks.withType(JavaCompile).configureEach {
    options.release = 21
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

dependencies {
    implementation 'jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api:3.0.0',
            'org.glassfish.web:jakarta.servlet.jsp.jstl:3.0.0',
            'org.glassfish:jakarta.json:2.0.1'
    providedCompile 'jakarta.servlet:jakarta.servlet-api:6.1.0'
}

tasks.register('combineCss', org.jlab.cat.CatTask) {
    from("src/main/webapp/resources/widgets").include('**/*.css')
    output = file("${buildDir}/combined-css/combined.css")
}

tasks.register('combineJs', org.jlab.cat.CatTask) {
    from("src/main/webapp/resources/widgets").include('**/*.js')
    output = file("${buildDir}/combined-js/combined.js")
}

tasks.register('cssMinifyIt', org.gradlewebtools.minify.CssMinifyTask) {
    dependsOn(combineCss)
    srcDir = project.file("build/combined-css")
    dstDir = project.file("build/minified")
}

tasks.register('jsMinifyIt', org.gradlewebtools.minify.JsMinifyTask) {
    dependsOn(combineJs)
    srcDir = project.file("build/combined-js")
    dstDir = project.file("build/minified")
}

tasks.named('jar') {
    enabled = false
}

war {
    dependsOn(cssMinifyIt)
    dependsOn(jsMinifyIt)
    archiveFileName = 'wedm.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
        filter {
            String line -> line.replaceAll("@PRODUCTION_RELEASE@", productionRelease)
        }
    }
    from('build/minified') {
        include '*.min.css'
        into 'resources/css/'
    }
    from('build/minified') {
        include '*.min.js'
        into 'resources/js/'
    }
}

spotless {
    java {
        googleJavaFormat()
    }
}